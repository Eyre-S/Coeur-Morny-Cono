plugins {
	id 'java'
	id 'java-library'
	id 'maven-publish'
	id 'application'
	id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group 'cc.sukazyo'
version '0.3.4.1'
project.ext.archiveBaseName = 'Coeur_Morny_Cono'
project.ext.artifactId = 'morny-coeur'
mainClassName = 'cc.sukazyo.cono.morny.MornyCoeur'

repositories {
	mavenCentral()
}

dependencies {
	
	compileOnlyApi "com.github.spotbugs:spotbugs-annotations:4.5.0"
	
	implementation 'com.github.pengrad:java-telegram-bot-api:5.3.0'
	
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
	
}

task updateVersionCode {
	ant.replaceregexp(match:'VERSION = ["a-zA-Z0-9.\\-_+@]+;', replace:"VERSION = \"$project.version\";", flags:'g', byline:true) {
		fileset(dir: 'src/main/java/cc/sukazyo/cono/morny', includes: 'MornySystem.java')
	}
}

compileJava.dependsOn updateVersionCode

test {
	useJUnitPlatform()
}

java {
	withSourcesJar()
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

tasks.withType(Javadoc) {
	options.encoding = 'UTF-8'
	options.docEncoding = 'UTF-8'
	options.charSet = 'UTF-8'
}

tasks.test {
	useJUnitPlatform()
}

shadowJar {
	archiveBaseName.set("${project.ext.archiveBaseName}")
	archiveVersion.set("${project.version}")
	archiveClassifier.set("fat")
}

publishing {
	repositories{
		maven {
			name 'builds'
			url = "file://" + new File("./build/publishing").getAbsolutePath()
		}
		maven {
			name '-ws-'
			url publishMvnRepoUrl
			credentials {
				username publishMvnRepoUsername
				password publishMvnRepoPassword
			}
		}
	}
	publications {
		main (MavenPublication) {
			from components.java
			groupId = project.group
			artifactId = project.ext.artifactId
			version = project.version
		}
	}
}
